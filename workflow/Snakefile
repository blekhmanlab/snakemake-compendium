report: "report/workflow.rst"
#configfile: "config/config.yaml"

with open('SraAccList.txt', 'r') as file:
    samples = file.read().splitlines()

shell.executable("/bin/sh") # sra-tools container doesn't have bash

rule all:
    input:
        'ASVs.fa', 'ASVs_counts.tsv', 'ASVs_taxonomy.tsv'

rule sra_prefetch:
    retries: 3
    input:
        'SraAccList.txt'
    output:
        temp('{sample}/{sample}.sra')
    container:
        'docker://ncbi/sra-tools:3.0.1'
    threads: 1
    resources:
        mem_mb=500,
        runtime=480
    shell:
        'prefetch {wildcards.sample}'

rule sra_to_fastq:
    input:
        '{sample}/{sample}.sra'
    output:
        'fastq/{sample}_1.fastq',
        'fastq/{sample}_2.fastq'
    container:
        'docker://ncbi/sra-tools:3.0.1'
    threads: 4
    resources:
        mem_mb=2000,
        runtime=480
    shell:
        'fasterq-dump --skip-technical --mem 1GB --threads 4 --split-files -O fastq/ {wildcards.sample}'

rule filter_paired:
    input:
        expand('fastq/{sample}_1.fastq', sample=samples),
        expand('fastq/{sample}_2.fastq', sample=samples),
        'dada2_1.14.0a.sif' #TODO: this is janky
    output:
        expand('intermediate/{sample}.R1.filtered.fastq.gz', sample=samples),
        expand('intermediate/{sample}.R2.filtered.fastq.gz', sample=samples)
    threads: 8
    resources:
        mem_mb=16000,
        runtime=1440
    shell:
        'singularity exec dada2_1.14.0a.sif Rscript scripts/paired_filter.R'
    #container:
    #    'docker://blekhmanlab/dada2:1.18.0'
    #shell:
    #    'Rscript scripts/process_paired_end.R'

rule errormodel_paired:
    input:
        expand('intermediate/{sample}.R1.filtered.fastq.gz', sample=samples),
        expand('intermediate/{sample}.R2.filtered.fastq.gz', sample=samples)
    output:
        'err_forward_reads.rds', 'err_reverse_reads.rds',
        'forward_error_model.pdf', 'reverse_error_model.pdf'
    threads: 8
    resources:
        mem_mb=16000,
        runtime=1440
    shell:
        'singularity exec dada2_1.14.0a.sif Rscript scripts/paired_error_models.R'

rule make_asv_table_paired:
    input:
        expand('intermediate/{sample}.R1.filtered.fastq.gz', sample=samples),
        expand('intermediate/{sample}.R2.filtered.fastq.gz', sample=samples),
        'err_forward_reads.rds', 'err_reverse_reads.rds'
    output:
        'summary.tsv', 'ASV.tsv', 'asv.rds'
    threads: 8
    resources:
        mem_mb=64000, # TODO: scale this based on sample count
        runtime=1440
    shell:
        'singularity exec dada2_1.14.0a.sif Rscript scripts/paired_generate_asvs.R'

rule assign_taxonomy:
    input:
        'asv.rds', 'resources/silva_nr99_v138_train_set.fa.gz'
    output:
        'ASVs.fa', 'ASVs_counts.tsv', 'ASVs_taxonomy.tsv'
    threads: 8
    resources:
        mem_mb=16000,
        runtime=1440
    shell:
        'singularity exec dada2_1.14.0a.sif Rscript scripts/assign_taxonomy.R'
